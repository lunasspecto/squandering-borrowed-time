Constant TASKS_PROVIDED;
Constant NUMBER_TASKS = 2;
Constant MAX_SCORE = 2;
Array task_scores -> 1 1;
Constant DEATH_MENTION_UNDO;
Constant DIALECT_US;
Constant MAX_MCCHOICES = 10;

Constant Story "Squandering borrowed time";
! El tÃ­tulo en castellano: "Despilfarrar tiempo prestado"
Constant Headline ", a game by Dominique Cypr@`es.^New players
    are encouraged to use the ~ABOUT~ command.^";
Release 1;
Include "Parser.h";
Include "yesno.h";
Include "shuffle.h";

Attribute walletsized;

Include "multichoice.inf";    
Include "messages.inf";
Include "songs.inf";

[ PrintTaskName task_number;
    switch (task_number)
    {
     0: "dropping the damn rock";
     1: "driving the hell away from that middle-of-nowhere observation
    point";
    }
];

[ PrintVerb word;
    switch (word) 
    {
     'score': print "check the score";
	return true;
     'xyzzy': print "cast the ~xyzzy~ spell";
 	return true;
     'about', 'info', 'informational', 'menu', 'hint', 'hints',
     'help': print "launch the informational menu";
	return true;
     'walkthrough', 'walkthru': print "obtain a walkthrough";
	return true;
     default: return false;
    }
];

Include "VerbLib.h";
Include "Grammar.h";
Include "notes.inf";
Include "about.inf";
Include "verbs.inf";
Include "intro.inf";
Include "navigation.inf";
Include "flashhope.inf";
Include "i93s.inf";

Object UWCampus "Somewhere on the UW campus"
 has 	light;

Object RestStop "A rest stop somewhere in Idaho"
 has 	light;

Object SumnerPlayground "A playground in Sumner, Washington"
 with 	description [; "You spent many school recesses here during the
 six years your family lived here. It was here that you first met Hope
 Andersson, or so I presume.", (note) 13, " Nothing much has changed
 in the intervening years, except that they seem to have got rid of a
 couple of the more dangerous pieces of equipment and replaced the
 swings. School is out today and everyone except you has somewhere
 better to be.^^The school lies to the east. To the west an expanse of
 tall grass stretches off some distance"; ],
	before [;
	 Listen:
	    if (noun == nothing) "There's little to hear. With
 night making its hesitant approach, though, you think you can make
 out the sound of frogs in the tall grass to the north, beyond
 everything.^^You remember once the volcanic eruption sirens were
 tested here. The piercing sounds, everyone gathering in groups,
 rehearsing the beginning of an uphill hike into Bonney Lake, though
 in reality no pyroclastic flow was hurtling into the valley from
 Mt. Rainier.";
	    return false;
	],
	u_to slide,
 has 	light;

Object -> swings "swings"
 with 	name 'swings' 'swing',
 has 	static pluralname;

Object -> slide "slide"
 with 	name 'slide',
	door_dir u_to,
	door_to HalfwaySlide,
 has 	static door ~openable open;

Object -> monkeybars "monkey bars"
 with 	name 'monkey' 'bars' 'monkeybars',
 has 	static pluralname;

Object -> playgroundgrass "tall grass"
 with 	name 'tall' 'grass',
 has 	scenery door open ~openable;

Object HalfwaySlide "Halfway up the slide"
 with 	description "The only way out is down.",
	d_to SumnerPlayground,
	after [;
	 Drop:
	    move noun to SumnerPlayground;
	    print (The) noun, " fall";
	    if (noun hasnt pluralname) print "s";
	    " down the slide and out of sight.";
	],
 has 	light;

Class 	SamuraiTortoise
 with 	name 'samurai' 'tortoise' 'action' 'figure' 'doll'
	    'figures//p' 'dolls//p' 'tortoises//p',
	short_name "Samurai Tortoise action figure",
	description "They don't make 'em like the used to.",
	plural "Samurai Tortoise action figures";

Object 	Storeroom "The storeroom"
 with 	description "The size of the sales floor is exhausting to
 	    contemplate, but the storeroom is a veritable city of
	    junk. At this deserted end of the place aisles of shelves
 	    rise to sickening heights, and atop them sit precarious
	    stacks of cardboard boxes from Vietnam, China,
	    Bangladesh...^^To the north is a small alcove where the
	    shelves end. To the south are other, busier facilities.
	    East and west lead into the shelving aisles.",
	s_to "And walk out on the first shift of your first real job?
	    Not likely.",
	e_to AisleTwentySeven,
	w_to AisleTwentyFour,
 has 	light;

Object 	-> ThreeTierCart "cart"
 with 	name 'red' 'wire' 'three' 'tier' 'three-tier' 'cart',
	description "A red wire three-tier cart of the kind reserved for
 	    employee use.",
	before [;
	 PushDir:
	    AllowPushDir();
	    return true;
	],
 has 	container static open ~openable;

SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;
SamuraiTortoise -> ->;

Object AisleTwentySeven "Aisle 27"
 with 	description "One of several aisles dedicated to toys.",
	w_to Storeroom,
 has 	light;

Object AisleTwentyFour "Aisle 24"
 with	description "An aisle for luggage.",
	e_to Storeroom,
 has 	light;

Object StoreroomAlcove "An alcove in the storeroom"
with description "Odds and ends are kept here at the far north end of
	    the storeroom.^^Aisles of shelving lead west and east. To
	    the south is the part of the storeroom from which you
	    came.",
	s_to Storeroom,
	w_to AisleTwentyFive,
	e_to AisleTwentySix,
 has 	light;

Object AisleTwentyFive "Aisle 25"
 with 	description "This aisle houses automotive goods.",
	e_to StoreroomAlcove,
 has 	light;

Object AisleTwentySix "Aisle 26"
 with 	description "An aisle for brightly colored pre-schoolers'
 toys.",
	w_to StoreroomAlcove,
 has 	light;


Include "initialise.inf";
